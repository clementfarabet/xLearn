# We refer to those directory underneath here
SET(3RDPARTY_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR})
SET(3RDPARTY_BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR})
SET(CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/cmake ${CMAKE_MODULE_PATH})

### Lua?
SET(OWN_LUA ON CACHE BOOL "3rd party software: install our own Lua")
IF(OWN_LUA)
  SET(LUA_SUBDIR "lua-5.1.3")
  ADD_SUBDIRECTORY(${LUA_SUBDIR})
ELSE(OWN_LUA)
  CONFIGURE_FILE("cmake/FindLua.cmake" "${Torch_BINARY_DIR}/cmake/FindLua.cmake" COPYONLY)
ENDIF(OWN_LUA)

### Cairo? (depends on zlib/libpng/pixman)

IF(NOT DEFINED OWN_CAIRO)
  FIND_PACKAGE(Cairo)
  IF(CAIRO_FOUND)
    SET(OWN_CAIRO OFF CACHE BOOL "3rd party software: install Cairo")
  ELSE(CAIRO_FOUND)
    # On Unix systems, Freetype2 and Fontconfig are mandatory
    IF(UNIX AND NOT APPLE)
      INCLUDE(${3RDPARTY_SOURCE_DIR}/cmake/FindFontConfig.cmake)
      INCLUDE(${3RDPARTY_SOURCE_DIR}/cmake/FindFreetype2.cmake)
      IF(FREETYPE2_FOUND AND FONTCONFIG_FOUND)
        SET(OWN_CAIRO ON CACHE BOOL "3rd party software: install Cairo")
      ELSE(FREETYPE2_FOUND AND FONTCONFIG_FOUND)
        MESSAGE(STATUS "Cairo cannot be compiled: fontconfig or freetype not installed")
        SET(OWN_CAIRO OFF CACHE BOOL "3rd party software: install Cairo")
      ENDIF(FREETYPE2_FOUND AND FONTCONFIG_FOUND)
    ELSE(UNIX AND NOT APPLE)
      SET(OWN_CAIRO ON CACHE BOOL "3rd party software: install Cairo")
    ENDIF(UNIX AND NOT APPLE)
  ENDIF(CAIRO_FOUND)
ENDIF(NOT DEFINED OWN_CAIRO)

IF(OWN_CAIRO)
  MESSAGE(STATUS "Cairo has not been detected on your system: we will compile it")
  SET(CAIRO_VERSION_MAJOR 1)
  SET(CAIRO_VERSION_MINOR 6)
  SET(CAIRO_VERSION_MICRO 4)
  
  SET(PIXMAN_VERSION_MAJOR 0)
  SET(PIXMAN_VERSION_MINOR 10)
  SET(PIXMAN_VERSION_MICRO 0)
  
  SET(LIBPNG_SUBDIR "libpng-1.2.26")
  SET(ZLIB_SUBDIR "zlib-1.2.3")
  SET(PIXMAN_SUBDIR "pixman-${PIXMAN_VERSION_MAJOR}.${PIXMAN_VERSION_MINOR}.${PIXMAN_VERSION_MICRO}")
  SET(CAIRO_SUBDIR "cairo-${CAIRO_VERSION_MAJOR}.${CAIRO_VERSION_MINOR}.${CAIRO_VERSION_MICRO}")

  ADD_SUBDIRECTORY(${ZLIB_SUBDIR})
  ADD_SUBDIRECTORY(${LIBPNG_SUBDIR})
  ADD_SUBDIRECTORY(${PIXMAN_SUBDIR})
  ADD_SUBDIRECTORY(${CAIRO_SUBDIR})

ELSE(OWN_CAIRO)
  CONFIGURE_FILE("cmake/FindCairo.cmake" "${Torch_BINARY_DIR}/cmake/FindCairo.cmake" COPYONLY)
ENDIF(OWN_CAIRO)
